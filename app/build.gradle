apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.chaquo.python'


android {
    compileSdkVersion COMPILE_SDKVERSION
    buildToolsVersion BUILDTOOLS_VERSION

    defaultConfig {
        applicationId "com.epiphany.callshow"
        minSdkVersion MINSDK_VERSION
        targetSdkVersion TARGETSDK_VERSION
        versionCode 1
        versionName getVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
        python {
            pip {
                install "js2py==0.71"
                install "requests==2.24.0"
                install "tqdm==4.62.3"
                install "pyquery==1.4.3"
                install "lxml"
                install "pydantic==1.4"
            }
        }
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        callshow {
            keyAlias = "callshow"
            keyPassword = "gaozhongkui"
            storeFile = file("./jks/call_show.jks")
            storePassword = "gaozhongkui"
            storeType = "jks"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.callshow
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.callshow
            minifyEnabled false
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    android.applicationVariants.all {variant ->
        variant.outputs.all {
            //在这里修改apk文件名，引号内的字符串都可以随便定义
            outputFileName = "Me_Hub_V${getVersionName()}_${buildTime()}.apk"
        }
    }

}

def buildTime(){
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmm')
    return formattedDate
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = stdout
        }
        def retVersionName = Integer.parseInt(stdout.toString().trim()) + 3000
        return '1.0.' + retVersionName
    }
    catch (ignored) {
        ignored.printStackTrace()
        return null
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    kapt 'com.github.bumptech.glide:compiler:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //下拉刷新库
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'
    implementation 'com.scwang.smart:refresh-header-classics:2.0.3'
    implementation 'com.scwang.smart:refresh-header-material:2.0.3'

    //引入Lottie的库
    implementation 'com.airbnb.android:lottie:3.5.0'

    //smart tab
    implementation 'com.ogaclejapan.smarttablayout:library:2.0.0@aar'


    //引入youtube相关的库
    implementation 'com.google.api-client:google-api-client-android:1.31.4'
    implementation 'com.google.apis:google-api-services-youtube:v3-rev20210410-1.31.0'

    //权限库
    implementation 'pub.devrel:easypermissions:3.0.0'

}
